buildscript {
  dependencies {
	classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta1'
	classpath 'commons-httpclient:commons-httpclient:3.1'
  }
}

plugins {
	id 'org.asciidoctor.convert' version '1.5.2'
	id 'com.github.jruby-gradle.base' version '0.1.5'
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'org.ysb33r.vfs'	

version = '1.0.0-SNAPSHOT'

repositories {
    jcenter()
}

ext {
	revealjsVersion = 'kubam'
	asciidoctorBackendVersion = 'kubam'
	downloadDir = new File(buildDir,'download')
	templateDir = new File(downloadDir,'templates')
	revealjsDir = new File(downloadDir,'reveal.js')
}

configurations {
	pdf
}

dependencies {
	pdf 'me.champeau.deck2pdf:deck2pdf:0.3.0'
}

task download << {
	mkdir downloadDir
	vfs {
		cp "zip:https://github.com/kubamarchwicki/asciidoctor-reveal.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
				templateDir, recursive:true, overwrite:true
		cp "zip:https://github.com/kubamarchwicki/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
                revealjsDir, recursive:true, overwrite:true
	}
}

download {
	description "Download extra revealjs resources"
	outputs.dir templateDir
	outputs.dir revealjsDir
}

asciidoctor {
	dependsOn download	

	sourceDir = file('src')

	sources {
		include '*.adoc'
	}
	
	resources {
		from (sourceDir) {
			include 'css/**'
			include 'images/**'
		}
		from (downloadDir) {
			include 'reveal.js/**'
		}		
	}

	requires = ['tilt']
	backends = ['revealjs']	

	options template_dir: new File(templateDir,'templates/slim').absolutePath
}

task generatePdf(type:JavaExec) {
  dependsOn asciidoctor
  main = 'me.champeau.deck2pdf.Main'
  workingDir = file("$buildDir/asciidoc/")
  args = ["$workingDir/revealjs/slides.html","$workingDir/pdf/${project.name}.pdf", '--profile=revealjs', '--width=968', '--height=547']
  classpath = configurations.pdf

  doFirst {
    file("$workingDir/pdf/").mkdirs()
  }
}